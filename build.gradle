
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'application'
apply plugin: 'groovy'

def javaVersion = '1.8'
sourceCompatibility = javaVersion
targetCompatibility = javaVersion
version = '1'
jar {
    manifest {
		attributes 'Implementation-Title': 'Pokeomon rip',
	   'Implementation-Version':version
   }
}
configurations {
    markdownDoclet
}

repositories {
    mavenCentral()
	maven { url "http://oss.sonatype.org/content/repositories/snapshots/" }
}

dependencies {
	markdownDoclet('ch.raffael.pegdown-doclet:pegdown-doclet:1.1.1')
	compile 'org.apache.commons:commons-lang3:3.3.2'
	compile 'org.slick2d:slick2d-core:1.0.1'
	compile 'org.codehaus.groovy:groovy-all:2.3.8'
	compile 'com.google.inject:guice:4.0-beta5'
	compile 'com.google.inject.extensions:guice-assistedinject:4.0-beta5'
	["spock-core", "spock-guice"].each{
        testCompile "org.spockframework:${it}:1.0-groovy-2.3-SNAPSHOT"
	}
}

javadoc.options {
    docletpath = configurations.markdownDoclet.files.asType(List) // gradle should relly make this simpler
	doclet = "ch.raffael.doclets.pegdown.PegdownDoclet"
	addStringOption("parse-timeout", "10")
}
def nativeDir = "$buildDir/natives"
task natives(type:Sync){
	def paths = new LinkedList()
    configurations.compile.each { file ->
        if (!file.name.contains("natives")) {
			return
        }
		zipTree(file).matching {
			exclude {
				it.path.contains('META-INF')
			}
		}.each {
			paths.add(it)
		}
    }
	from paths into nativeDir
}
tasks.withType(Test) {
	dependsOn natives
	systemProperty "java.library.path", "$buildDir/libs"
}
eclipse.classpath.file {
    whenMerged { classpath ->
		def entry = classpath.entries.find{
			it.path.contains("lwjgl")
		}
		entry.nativeLibraryLocation=nativeDir
	}
}
